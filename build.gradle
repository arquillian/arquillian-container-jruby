buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        classpath 'io.spring.gradle:dependency-management-plugin:0.5.2.RELEASE'
    }
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply from: 'gradle/publish.gradle'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenLocal()
    maven { url 'http://rubygems-proxy.torquebox.org/releases' }
}

version = '1.0.0.Alpha3-SNAPSHOT'

ext {
    version_arquillian          = '1.1.13.Final'
    version_junit               = '4.12'
    version_hamcrest            = '1.3'
    version_jruby               = '1.7.20.1'
    version_commons_io          = '2.2'
    version_mockito             = '1.10.19'
}

repositories {
    jcenter()
}

configurations {
    testgems
    provided
}

dependencyManagement {
    imports {
        mavenBom "org.jboss.arquillian:arquillian-bom:${version_arquillian}"
    }
    generatedPomCustomization {
        importedBomAction = 'copy'
    }
}

dependencies {
    compile group: 'org.jboss.arquillian.container', name: 'arquillian-container-spi'
    compile group: 'org.jboss.arquillian.container', name: 'arquillian-container-test-spi'
    compile group: 'org.jboss.arquillian.container', name: 'arquillian-container-impl-base'
    compile group: 'org.jboss.arquillian.container', name: 'arquillian-container-test-impl-base'

    provided group: 'org.jruby', name: 'jruby-complete', version: version_jruby

    testCompile group: 'org.jboss.arquillian.junit', name: 'arquillian-junit-container'
    testCompile group: 'org.jboss.shrinkwrap.resolver', name: 'shrinkwrap-resolver-depchain'
    testCompile group: 'junit', name: 'junit', version: version_junit
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: version_hamcrest
    testCompile group: 'commons-io', name: 'commons-io', version: version_commons_io
    testCompile group: 'org.mockito', name: 'mockito-all', version: version_mockito
    testCompile group: 'org.jruby', name: 'jruby-complete', version: version_jruby

    testgems group: 'rubygems', name: 'asciidoctor', version: '2.0.7'
}


compileJava {
    classpath += configurations.provided
}

test {

    filter {
        includeTestsMatching 'org.arquillian.jruby.gems.*'
        includeTestsMatching 'org.arquillian.jruby.scripts.*'
    }

}

task integrationTestUncached(type: Test) {

    systemProperty 'arquillian.launch', 'jrubyUncached'

    filter {
        includeTestsMatching '*.JRubyUncachedTest'
    }

}

task integrationTestCached(type: Test) {

    systemProperty 'arquillian.launch', 'jrubyCached'
    filter {
        includeTestsMatching '*.JRubyCachedTest'
    }

}

task integrationTestIsolatedClassloader(type: Test) {

    systemProperty 'arquillian.launch', 'isolated'
    filter {
        includeTestsMatching '*.JRubyIsolatedClassloaderTest'
    }

}

check.dependsOn integrationTestUncached
check.dependsOn integrationTestCached
check.dependsOn integrationTestIsolatedClassloader

